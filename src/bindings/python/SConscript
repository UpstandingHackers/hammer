# -*- python -*-
import os.path
Import('env libhammer_shared')

pythonenv = env.Clone(IMPLICIT_COMMAND_DEPENDENCIES = 0)

pythonenv.Append(CPPPATH = ['../../', '/usr/include/python2.7'])
pythonenv.Append(CCFLAGS = ['-fpic', '-DSWIG', '-Wno-all', '-Wno-extra', '-Wno-error'])
pythonenv.Append(LIBS = ['hammer'])
pythonenv.Append(LIBPATH = ['../../'])
pythonenv.Append(SWIGFLAGS = ['-DHAMMER_INTERNAL__NO_STDARG_H', '-Isrc/', '-python'])

pythonenv.Command("hammer.i", "../swig/hammer.i", Copy("$TARGET", "$SOURCE"))

swig = ['hammer.i']

libhammer_python = pythonenv.SharedLibrary('hammer', swig, SHLIBPREFIX='_')

pytestenv = pythonenv.Clone()
pytestenv['ENV']['LD_LIBRARY_PATH'] = os.path.dirname(str(libhammer_shared[0]))
pytestenv.Command(None, ['hammer_tests.py'] + libhammer_python, "nosetests -vv $SOURCE")

Clean('.', ['hammer.pyc', 'hammer_tests.py', 'hammer_tests.pyc']) 
